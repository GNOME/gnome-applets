AC_INIT(drivemount/drivemount.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-applets, 2.2.0)

AM_MAINTAINER_MODE

AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")
AC_PATH_XTRA
X_LIBS="$X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS"
AC_SUBST(X_LIBS)

GNOME_COMPILE_WARNINGS(yes)

dnl Stuff we might need, not quite sure yet

GTK_REQUIRED=2.1.0
LIBGNOME_REQUIRED=2.0.0
LIBGNOMEUI_REQUIRED=2.0.0
GNOME_VFS_REQUIRED=1.9.16
GCONF_REQUIRED=1.1.11
SCROLLKEEPER_REQUIRED=0.1.4
LIBPANEL_REQUIRED=2.0.0
LIBGTOP_REQUIRED=2.0.0
LIBWNCK_REQUIRED=0.13
LIBGLADE_REQUIRED=2.0.0

dnl configure argument fo disable schema install

AC_ARG_ENABLE(install_schemas,
	[  --disable-install-schemas        Disable installation of the gconf schemas])
AM_CONDITIONAL(INSTALL_SCHEMAS, test x$enable_install_schemas != xno)

dnl ******************************************
dnl ** pkg-config dependacy checks          **
dnl ******************************************

PKG_CHECK_MODULES(GAILUTIL, gail >= 0.13)
AC_SUBST(GAILUTIL_CFLAGS)
AC_SUBST(GAILUTIL_LIBS)

PKG_CHECK_MODULES(GNOME_APPLETS, gtk+-2.0 >= $GTK_REQUIRED libpanelapplet-2.0 >= $LIBPANEL_REQUIRED)
AC_SUBST(GNOME_APPLETS_CFLAGS)
AC_SUBST(GNOME_APPLETS_LIBS)

PKG_CHECK_MODULES(GNOME_LIBS2, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED)
AC_SUBST(GNOME_LIBS2_CFLAGS)
AC_SUBST(GNOME_LIBS2_LIBS)

build_gnome_vfs_applets=false
PKG_CHECK_MODULES(GNOME_VFS_APPLETS, 
		  [gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED],
		  build_gnome_vfs_applets=true,)
AC_SUBST(GNOME_VFS_APPLETS_CFLAGS)
AC_SUBST(GNOME_VFS_APPLETS_LIBS)
AM_CONDITIONAL(BUILD_GNOME_VFS_APPLETS, $build_gnome_vfs_applets)

PKG_CHECK_MODULES(LIBGLADE, libglade-2.0 >= $LIBGLADE_REQUIRED)
AC_SUBST(LIBGLADE_CFLAGS)
AC_SUBST(LIBGLADE_LIBS)

AC_PATH_PROG(GDK_PIXBUF_CSOURCE, gdk-pixbuf-csource, no)

if test x"$GDK_PIXBUF_CSOURCE" = xno; then
  AC_MSG_ERROR([gdk-pixbuf-csource executable not found in your path - should be installed with GTK])
fi 
  
AC_SUBST(GDK_PIXBUF_CSOURCE)

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test x"$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2


dnl Still need to decide on the checks for these

build_gtop_applets=false
PKG_CHECK_MODULES(GTOP_APPLETS,
                 [libgtop-2.0 >= $LIBGTOP_REQUIRED],
                 build_gtop_applets=true,
		 AC_MSG_WARN([libgtop not found. Not building gtop applets.]))
AC_SUBST(GTOP_APPLETS_CFLAGS)
AC_SUBST(GTOP_APPLETS_LIBS)
AM_CONDITIONAL(BUILD_GTOP_APPLETS, $build_gtop_applets)

dnl ************************
dnl **   Set up gettext   **
dnl ************************
ALL_LINGUAS="am ar az be bg ca cs da de el en_GB es et eu fi fr ga gl he hi hu it ja ko lt lv mn ms nl nn no pl pt pt_BR ro ru sk sl sv ta tr uk vi wa zh_CN zh_TW"

GETTEXT_PACKAGE=gnome-applets-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE")

AM_GLIB_GNU_GETTEXT

# this is the directory where the *.{mo,gmo} files are installed
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

dnl ******************************************
dnl ** Check for Scrollkeeper               **
dnl ******************************************

AC_PATH_PROG(SCROLLKEEPER_CONFIG, scrollkeeper-config, no)
if test x$SCROLLKEEPER_CONFIG = xno; then
  AC_MSG_ERROR(Couldn't find scrollkeeper-config, please install the Scrollkeeper 0.1.4 package)
fi
AC_SUBST(SCROLLKEEPER_REQUIRED)

dnl ******************************************
dnl ******************************************


dnl ******************************************
dnl **  Test whether jw is installed        **
dnl ******************************************

AC_PATH_PROG(JW, jw, no)
if test x$JW = xno; then
  HAVE_JW="no"
else
  HAVE_JW="yes"
fi
AM_CONDITIONAL(HAVE_JW, test "x$HAVE_JW" = "xyes")
AC_SUBST(HAVE_JW)

dnl ******************************************
dnl ** cdplayer applet check                **
dnl ******************************************
dnl FIXME: What's the default device for irix? and is this correct for Netbsd
dnl AC_CANONICAL_HOST
cdplayer_type_sun=false
case "$host" in
*irix*) CDROM_HOST=irix; CDROM_LIBS="-lcdaudio -lmediad -lds";cd_device_path="/dev/cdrom";;
*linux*) CDROM_HOST=linux;cd_device_path="/dev/cdrom";;
*netbsd*) CDROM_HOST=netbsd;cd_device_path="/dev/rcd0";;
*solaris*) CDROM_HOST=solaris;cd_device_path="/vol/dev/aliases/cdrom0";cdplayer_type_sun=true;;
esac
case "$host" in
sparc-*-solaris*)	gkb_type_sun=true;;
*)			gkb_type_sun=false;;
esac
AM_CONDITIONAL(GKB_SUN, $gkb_type_sun)
AM_CONDITIONAL(CDDA_SOLARIS, $cdplayer_type_sun)

AM_CONDITIONAL(APPLET_CDPLAYER, test -n "$CDROM_HOST")
AC_SUBST(CDROM_HOST)
AC_SUBST(CDROM_LIBS)
AC_DEFINE_UNQUOTED(DEV_PATH, "$cd_device_path")
if test -z "$CDROM_HOST"; then
dnl  
  AC_MSG_WARN([*** \`cdplayer' applet will not be built ***])
fi

dnl ******************************************
dnl ******************************************


dnl ******************************************
dnl ** battstat check			    **
dnl ******************************************

build_battstat_applet=yes
case "${host}" in
  powerpc-*-linux*)
  	HAVE_LIBAPM=no
  	AM_CONDITIONAL(HAVE_LIBAPM, test "x$HAVE_LIBAPM" = "xyes")
	AC_SUBST(HAVE_LIBAPM)
	;;
  *-*-linux*)
	AC_CHECK_LIB(apm,apm_read,[HAVE_LIBAPM=yes],[HAVE_LIBAPM=no])
	AM_CONDITIONAL(HAVE_LIBAPM, test "x$HAVE_LIBAPM" = "xyes")
	AC_SUBST(HAVE_LIBAPM)  
	;;
  *solaris*)
	build_battstat_applet=no
	;;
esac

AM_CONDITIONAL(BUILD_BATTSTAT_APPLET, test x$build_battstat_applet = xyes)

dnl *******************************************
dnl ** mixer applet check                    **
dnl *******************************************

P_MIXER=
MIXER_LIBS=
AC_CHECK_HEADERS(linux/soundcard.h,[P_MIXER=mixer],[
  AC_CHECK_HEADERS(machine/soundcard.h, [P_MIXER=mixer],[
    AC_CHECK_HEADERS(sys/soundcard.h, [P_MIXER=mixer],[
      AC_CHECK_HEADERS(soundcard.h, [P_MIXER=mixer; MIXER_LIBS="-lossaudio"],[
        AC_CHECK_HEADERS(sys/audioio.h, [P_MIXER=mixer],[
          AC_CHECK_HEADERS(sys/audio.io.h, [P_MIXER=mixer],[
            AC_CHECK_HEADERS(sun/audioio.h, [P_MIXER=mixer],[
	      AC_CHECK_HEADERS(dmedia/audio.h, [P_MIXER=mixer]),]) ]) ]) ]) ]) ]) ])
AC_SUBST(MIXER_LIBS)

AM_CONDITIONAL(APPLET_MIXER, test "$P_MIXER" = "mixer")
if test "$P_MIXER" != "mixer"; then
  AC_MSG_WARN([*** \`mixer' applet will not be built ***])
fi

dnl ******************************************
dnl ******************************************



dnl ******************************************
dnl ** modemlights applet check             **
dnl ******************************************

P_MODEMLIGHTS=
case "$host_os" in
linux*|freebsd*|netbsd*|openbsd*)
  AC_CHECK_HEADERS(net/ppp_defs.h,[P_MODEMLIGHTS=modemlights])
  ;;
esac
AM_CONDITIONAL(APPLET_MODEMLIGHTS, test "$P_MODEMLIGHTS" = "modemlights")
if test "$P_MODEMLIGHTS" != "modemlights"; then
  AC_MSG_WARN([*** \`modemlights' applet will not be built ***])
fi

dnl ***********************************************
dnl ** PanelMenu applet                          **
dnl ***********************************************

AC_ARG_ENABLE(panelmenu, 
  [  --enable-panelmenu=[no/yes]		Build panel-menu applet [ default=no ]],
   enable_panelmenu="$enableval", enable_panelmenu=no)


AM_CONDITIONAL(BUILD_PANEL_MENU, test "$enable_panelmenu" = yes)

if test x$enable_panelmenu = xyes; then
  PKG_CHECK_MODULES(PANEL_MENU, gtk+-2.0 >= $GTK_REQUIRED libwnck-1.0 >= $LIBWNCK_REQUIRED libpanelapplet-2.0 >= $LIBPANEL_REQUIRED gconf-2.0 >= $GCONF_REQUIRED gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED)
  AC_SUBST(PANEL_MENU_CFLAGS)
  AC_SUBST(PANEL_MENU_LIBS)
fi

# Honor aclocal flags
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AC_DEFINE_UNQUOTED(GNOME_ICONDIR, "${prefix}/share/pixmaps")

#defined the below to enable help to work for applets 

AC_DEFINE_UNQUOTED(DATADIR, "${prefix}/share")
AC_DEFINE_UNQUOTED(SYSCONFDIR, "${prefix}/etc")
AC_DEFINE_UNQUOTED(LIBDIR, "${prefix}/lib")
AC_DEFINE_UNQUOTED(PREFIX, "$prefix")

AC_OUTPUT([
gnome-applets.spec
Makefile
po/Makefile.in
screen-exec/Makefile
mini-commander/Makefile
mini-commander/src/Makefile
mini-commander/help/Makefile
mini-commander/help/C/Makefile
mixer/Makefile
mixer/docs/Makefile
mixer/docs/C/Makefile
drivemount/Makefile
drivemount/help/Makefile
drivemount/help/C/Makefile
modemlights/Makefile
cdplayer/Makefile
cdplayer/docs/Makefile
cdplayer/docs/C/Makefile
multiload/Makefile
gkb-new/Makefile
gkb-new/gkb/Makefile
gkb-new/xmodmap/Makefile
gkb-new/descs/Makefile
gkb-new/xmodmap.sun/Makefile
gkb-new/descs.sun/Makefile
gkb-new/gkb_xmmap.in
gkb-new/help/Makefile
gkb-new/help/C/Makefile
panel-menu/Makefile
panel-menu/help/Makefile
panel-menu/help/C/Makefile
charpick/Makefile
charpick/help/Makefile
charpick/help/C/Makefile
gtik/Makefile
gtik/access/Makefile
gtik/help/Makefile
gtik/help/C/Makefile
gweather/Makefile
gweather/pixmaps/Makefile
gweather/docs/Makefile
gweather/docs/C/Makefile
geyes/Makefile
geyes/themes/Makefile
geyes/docs/Makefile
geyes/docs/C/Makefile
battstat/Makefile
battstat/apmlib/Makefile
battstat/sounds/Makefile
battstat/docs/Makefile
battstat/docs/C/Makefile
man/Makefile
wireless/Makefile
omf-install/Makefile
])
