AC_INIT(drivemount/drivemount.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-applets, 2.9.3.2)

AM_MAINTAINER_MODE

AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")
AC_PATH_XTRA
X_LIBS="$X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS"
AC_SUBST(X_LIBS)

GNOME_COMPILE_WARNINGS

dnl Stuff we might need, not quite sure yet

GTK_REQUIRED=2.5.0
GLIB_REQUIRED=2.5.0
LIBGNOME_REQUIRED=2.8.0
LIBGNOMEUI_REQUIRED=2.8.0
GNOME_VFS_REQUIRED=2.8.0
GCONF_REQUIRED=2.8.0
SCROLLKEEPER_REQUIRED=0.1.4
LIBPANEL_REQUIRED=2.8.0
LIBGTOP_REQUIRED=2.8.0
LIBGLADE_REQUIRED=2.4.0
LIBGAIL_REQUIRED=1.1.0
LIBXKLAVIER_REQUIRED=1.13

dnl ******************************************
dnl ** pkg-config dependacy checks          **
dnl ******************************************

PKG_CHECK_MODULES(GAILUTIL, gail >= $LIBGAIL_REQUIRED)
AC_SUBST(GAILUTIL_CFLAGS)
AC_SUBST(GAILUTIL_LIBS)

PKG_CHECK_MODULES(GNOME_APPLETS, gtk+-2.0 >= $GTK_REQUIRED libpanelapplet-2.0 >= $LIBPANEL_REQUIRED)
AC_SUBST(GNOME_APPLETS_CFLAGS)
AC_SUBST(GNOME_APPLETS_LIBS)

PKG_CHECK_MODULES(GNOME_LIBS2, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED)
AC_SUBST(GNOME_LIBS2_CFLAGS)
AC_SUBST(GNOME_LIBS2_LIBS)

build_gnome_vfs_applets=false
PKG_CHECK_MODULES(GNOME_VFS_APPLETS, 
		  [gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED],
		  build_gnome_vfs_applets=true,)
AC_SUBST(GNOME_VFS_APPLETS_CFLAGS)
AC_SUBST(GNOME_VFS_APPLETS_LIBS)
AM_CONDITIONAL(BUILD_GNOME_VFS_APPLETS, $build_gnome_vfs_applets)

PKG_CHECK_MODULES(LIBGLADE, libglade-2.0 >= $LIBGLADE_REQUIRED)
AC_SUBST(LIBGLADE_CFLAGS)
AC_SUBST(LIBGLADE_LIBS)

AC_PATH_PROG(GDK_PIXBUF_CSOURCE, gdk-pixbuf-csource, no)

if test x"$GDK_PIXBUF_CSOURCE" = xno; then
  AC_MSG_ERROR([gdk-pixbuf-csource executable not found in your path - should be installed with GTK])
fi 
  
AC_SUBST(GDK_PIXBUF_CSOURCE)

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test x"$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

dnl checking for xsltproc

AC_PATH_PROG(XSLTPROC, xsltproc, no)

if test x"$XSLTPROC" = xno; then
  AC_MSG_ERROR([xsltproc executable not found in your path - should be installed with libxslt])
fi

dnl Still need to decide on the checks for these

build_gtop_applets=false
PKG_CHECK_MODULES(GTOP_APPLETS,
                 [libgtop-2.0 >= $LIBGTOP_REQUIRED],
                 build_gtop_applets=true,
		 AC_MSG_WARN([libgtop not found. Not building gtop applets.]))
AC_SUBST(GTOP_APPLETS_CFLAGS)
AC_SUBST(GTOP_APPLETS_LIBS)
AM_CONDITIONAL(BUILD_GTOP_APPLETS, $build_gtop_applets)

dnl Setup for libxklavier
PKG_CHECK_MODULES(LIBXKLAVIER, libxklavier >= $LIBXKLAVIER_REQUIRED,,
    AC_MSG_ERROR([libxklavier was not found and is required to build gnome-applets.
Please get the latest version from http://freedesktop.org/Software/LibXklavier], 1))

AC_SUBST(LIBXKLAVIER_CFLAGS)
AC_SUBST(LIBXKLAVIER_LIBS)

AC_ARG_ENABLE(flags,
[  --enable-flags       Enable flags usage in the keyboard indicators],
, enable_flags=no)
AM_CONDITIONAL(ENABLE_FLAGS, test x$enable_flags != xno)

dnl ==============
dnl gswitchit
dnl ==============
dnl Make sure the new cvs virtual include has been checked out
AC_MSG_CHECKING([for inclusion of libgswitchit])
if test ! -d "$srcdir/libgswitchit"; then
    AC_MSG_RESULT(no)
    AC_MSG_ERROR([Your checkout of gnome-applets is out of date.
    The 'libgswitchit' module has been virtually included into
    gnome-applets by CVS as a subdirectory.  Please check
    out again (this can be done on top of your existing checkout])
else
   AC_MSG_RESULT(yes)
fi
AM_CONDITIONAL(INCLUDE_GSWITCHIT_DEVEL, true)  dnl pull devel from applet

dnl ************************
dnl **   Set up gettext   **
dnl ************************
ALL_LINGUAS="am ar az be bg bn bs ca cs cy da de el en_CA en_GB es et eu fa fi fr ga gl gu he hi hr hu id is it ja ko lt lv mk ml mn ms nb nl nn no pa pl pt pt_BR ro ru sk sl sq sr sr@Latn sv ta th tr uk vi wa zh_CN zh_TW"

GETTEXT_PACKAGE=gnome-applets-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Gettext Package])
AC_DEFINE_UNQUOTED(GNOMELOCALEDIR, "${datadir}/locale", [locale directory])
AM_GLIB_GNU_GETTEXT

# this is the directory where the *.{mo,gmo} files are installed
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

dnl ******************************************
dnl ** Check for Scrollkeeper               **
dnl ******************************************

AC_PATH_PROG(SCROLLKEEPER_CONFIG, scrollkeeper-config, no)
if test x$SCROLLKEEPER_CONFIG = xno; then
  AC_MSG_ERROR(Couldn't find scrollkeeper-config, please install the Scrollkeeper 0.1.4 package)
fi
AC_SUBST(SCROLLKEEPER_REQUIRED)

dnl ******************************************
dnl ******************************************


dnl ******************************************
dnl **  Test whether jw is installed        **
dnl ******************************************

AC_PATH_PROG(JW, jw, no)
if test x$JW = xno; then
  HAVE_JW="no"
else
  HAVE_JW="yes"
fi
AM_CONDITIONAL(HAVE_JW, test "x$HAVE_JW" = "xyes")
AC_SUBST(HAVE_JW)

dnl ******************************************
dnl ** cdplayer applet check                **
dnl ******************************************
dnl FIXME: What's the default device for irix? and is this correct for Netbsd
dnl AC_CANONICAL_HOST
cdplayer_type_sun=false
case "$host" in
*irix*) CDROM_HOST=irix; CDROM_LIBS="-lcdaudio -lmediad -lds";cd_device_path="/dev/cdrom";;
*linux*) CDROM_HOST=linux;cd_device_path="/dev/cdrom";;
*netbsd*) CDROM_HOST=netbsd;cd_device_path="/dev/rcd0";;
*solaris*) CDROM_HOST=solaris;cd_device_path="/vol/dev/aliases/cdrom0";cdplayer_type_sun=true;;
esac
case "$host" in
sparc-*-solaris*)	gkb_type_sun=true;;
*)			gkb_type_sun=false;;
esac
AM_CONDITIONAL(GKB_SUN, $gkb_type_sun)
AM_CONDITIONAL(CDDA_SOLARIS, $cdplayer_type_sun)

AM_CONDITIONAL(APPLET_CDPLAYER, test -n "$CDROM_HOST")
AC_SUBST(CDROM_HOST)
AC_SUBST(CDROM_LIBS)
AC_DEFINE_UNQUOTED(DEV_PATH, "$cd_device_path", [CD Device Path])
if test -z "$CDROM_HOST"; then
dnl  
  AC_MSG_WARN([*** \`cdplayer' applet will not be built ***])
fi

dnl ******************************************
dnl ******************************************


dnl ******************************************
dnl ** battstat check			    **
dnl ******************************************

dnl --disable-battstat=(yes|no)
AC_ARG_ENABLE(battstat,
	      [  --disable-battstat=[no/yes] don't build battery monitor applet [default=no]],,
	      disable_battstat=no)

HAVE_LIBAPM=no
NEED_LIBAPM=no
build_battstat_applet=no

if test x$disable_battstat = xno; then
   build_battstat_applet=yes
   case "${host}" in
       powerpc-*-linux*)
           NEED_LIBAPM=yes
           AC_CHECK_LIB(apm,apm_read,[HAVE_LIBAPM=yes],[HAVE_LIBAPM=no])
	   ;;
       # list of supported OS cores that use libapm
       *-*-linux*)
	   NEED_LIBAPM=yes
	   AC_CHECK_LIB(apm,apm_read,[HAVE_LIBAPM=yes],[HAVE_LIBAPM=no])
	   ;;
       # list of supported OS cores that do not use libapm
       *-*-freebsd*|*-*-netbsd*|*-*-openbsd*)
	   ;;
       *)
	   echo "warning: ${host} is not supported by battstat_applet, not building" >&2
	   build_battstat_applet=no
	   ;;
    esac
fi

AC_SUBST(HAVE_LIBAPM)
AC_SUBST(NEED_LIBAPM)
AM_CONDITIONAL(HAVE_LIBAPM, test "x$HAVE_LIBAPM" = "xyes")
AM_CONDITIONAL(NEED_LIBAPM, test "x$NEED_LIBAPM" = "xyes")
AM_CONDITIONAL(BUILD_BATTSTAT_APPLET, test x$build_battstat_applet = xyes)

dnl *******************************************
dnl ** mixer applet check                    **
dnl *******************************************

P_MIXER=
MIXER_CFLAGS=
MIXER_LIBS=

GST_MAJORMINOR=0.8
GST_REQ=0.8.2
PKG_CHECK_MODULES(MIXER, gstreamer-interfaces-$GST_MAJORMINOR >= $GST_REQ \
			 gstreamer-$GST_MAJORMINOR >= $GST_REQ,
		  HAVE_GST="yes", HAVE_GST="no")
if test "x$HAVE_GST" = "xyes"; then
  P_MIXER=mixer
fi

AC_SUBST(MIXER_CFLAGS)
AC_SUBST(MIXER_LIBS)

AM_CONDITIONAL(APPLET_MIXER, test "$P_MIXER" = "mixer")
if test x"$P_MIXER" != x"mixer"; then
  AC_MSG_WARN([*** 'mixer' applet will not be built ***])
fi

GUCHARMAP_CFLAGS=
GUCHARMAP_LIBS=
PKG_CHECK_MODULES(GUCHARMAP, gucharmap >= 1.4.0,
		  HAVE_GUCHARMAP="yes", HAVE_GUCHARMAP="no")

if test "x$HAVE_GUCHARMAP" = "xyes"; then
	AC_DEFINE(HAVE_GUCHARMAP, 1, [Gucharmap Available])
else
  AC_MSG_WARN([*** 'charpick' applet will not be built with gucharmap support ***])
fi
AC_SUBST(GUCHARMAP_CFLAGS)
AC_SUBST(GUCHARMAP_LIBS)

dnl ******************************************
dnl ** keyboard accessibility status applet check
dnl ******************************************

savecppflags=$CPPFLAGS
CPPFLAGS="$CPPFLAGS $X_CFLAGS"
AC_CHECK_HEADER(X11/extensions/XKB.h, HAVE_XKB=true, HAVE_XKB=false)

AM_CONDITIONAL(APPLET_ACCESSX, $HAVE_XKB)
if test "x$APPLET_ACCESSX" = "xtrue"; then
  AC_MSG_WARN([*** \`keyboard accessibility status' applet will not be built ***])
fi
CPPFLAGS=$savecppflags


AM_CONDITIONAL(APPLET_GSWITCHIT, $HAVE_XKB)
if test "x$HAVE_XKB" != "xtrue"; then
  AC_MSG_WARN([*** \`GSwitchIt' applet will not be built ***])
fi

dnl ******************************************
dnl ** modemlights applet check             **
dnl ******************************************

P_MODEMLIGHTS=
case "$host_os" in
linux*|freebsd*|netbsd*|openbsd*)
  AC_CHECK_HEADERS(net/ppp_defs.h,[P_MODEMLIGHTS=modemlights])
  ;;
esac
AM_CONDITIONAL(APPLET_MODEMLIGHTS, test "$P_MODEMLIGHTS" = "modemlights")
if test "$P_MODEMLIGHTS" != "modemlights"; then
  AC_MSG_WARN([*** \`modemlights' applet will not be built ***])
fi

dnl ******************************************
dnl ** cpufreq applet check                 **
dnl ******************************************

dnl --disable-cpufreq=(yes|no)
AC_ARG_ENABLE(cpufreq,
	      [  --disable-cpufreq[=no/yes] don't build cpu frequency scaling monitor applet [default=no]],,
	      disable_cpufreq=no)

dnl --enable-frequency-selector=(yes/no)
AC_ARG_ENABLE(frequency-selector,
	      [  --enable-frequency-selector[=yes/no] build frequency selector [default yes]], ,
	      enable_selector=yes)

dnl --enable-suid=(yes/no)
AC_ARG_ENABLE(suid,
	      [  --enable-suid[=yes/no] suid root the frequency selector executable [default yes]], ,
	      suid=yes)

PKG_CHECK_MODULES(CPUFREQ_SELECTOR, glib-2.0 >= $GLIB_REQUIRED gobject-2.0 >= $GLIB_REQUIRED)
AC_SUBST(CPUFREQ_SELECTOR_CFLAGS)
AC_SUBST(CPUFREQ_SELECTOR_LIBS)

build_cpufreq_applet=no

if test x$disable_cpufreq = xno; then
   case "${host}" in
      *linux*)
         build_cpufreq_applet=yes
	 ;;
      *)
         echo "warning: ${host} is not supported by cpufreq applet, not building" >&2
	 build_cpufreq_applet=no
	 ;;
   esac
fi

AM_CONDITIONAL(BUILD_CPUFREQ_APPLET, test x$build_cpufreq_applet = xyes)
AM_CONDITIONAL(BUILD_CPUFREQ_SELECTOR, test x$enable_selector = xyes)
AM_CONDITIONAL(CPUFREQ_SELECTOR_SUID, test x$suid = xyes)


dnl ====================================================================
dnl Code for checking whether IPv6 is to be enabled on the system.
dnl ====================================================================
AC_MSG_CHECKING([whether to enable ipv6])
AC_ARG_ENABLE(ipv6, [ --enable-ipv6 enable IPv6 extensions], ,enable_ipv6=yes)
if test $enable_ipv6 = yes; then

  dnl ====================================================================
  dnl Code for checking presence of AF_INET6 on the system.
  dnl ====================================================================
  AC_TRY_COMPILE([
    #include <sys/types.h>
    #include <sys/socket.h>], [
    socket(AF_INET6, SOCK_STREAM, 0)
    ],
    have_ipv6=yes,
    have_ipv6=no
  )
  AC_MSG_RESULT($have_ipv6)
  if test $have_ipv6 = yes; then
      AC_DEFINE(ENABLE_IPV6, 1, [Define if system is IPv6 enabled])
  fi
fi

dnl =================================================================
dnl Now we would check for specific function like getaddrinfo.
dnl =================================================================
have_getaddrinfo=no
AC_CHECK_FUNC(getaddrinfo, have_getaddrinfo=yes)
if test $have_getaddrinfo != yes; then
  # getaddrinfo is not in the default libraries.  See if it's in some other.
  for lib in bsd socket inet; do
    AC_CHECK_LIB($lib, getaddrinfo, [LIBS="$LIBS -l$lib";have_getaddrinfo=yes; break])
  done
fi
if test $have_getaddrinfo = yes; then
  AC_DEFINE(HAVE_GETADDRINFO, 1, [Define if getaddrinfo() exists on the system])
fi

dnl =================================================================
dnl End of IPv6 checks
dnl =================================================================

# Honor aclocal flags
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

iconsdir="${datadir}/icons"
AC_SUBST(iconsdir)
pixmapsdir="${datadir}/pixmaps"
AC_SUBST(pixmapsdir)

#defined the below to enable help to work for applets 

AC_DEFINE_UNQUOTED(GNOME_ICONDIR, "${iconsdir}", [Gnome Icon Directory])
AC_DEFINE_UNQUOTED(GNOME_PIXMAPSDIR, "${datadir}/pixmaps", [Gnome Pixmaps Directory])
AC_DEFINE_UNQUOTED(DATADIR, "${prefix}/share", [datadir])
AC_DEFINE_UNQUOTED(SYSCONFDIR, "${prefix}/etc", [sysconfdir])
AC_DEFINE_UNQUOTED(LIBDIR, "${prefix}/lib", [libdir])
AC_DEFINE_UNQUOTED(PREFIX, "$prefix", [install prefix])

AC_OUTPUT([
gnome-applets.spec
Makefile
po/Makefile.in
accessx-status/Makefile
accessx-status/docs/Makefile
accessx-status/docs/C/Makefile
accessx-status/docs/eu/Makefile
accessx-status/pixmaps/Makefile
mini-commander/Makefile
mini-commander/src/Makefile
mini-commander/help/Makefile
mini-commander/help/C/Makefile
mini-commander/help/de/Makefile
mini-commander/help/es/Makefile
mini-commander/help/eu/Makefile
mini-commander/help/fr/Makefile
mini-commander/help/it/Makefile
mini-commander/help/ja/Makefile
mini-commander/help/ko/Makefile
mini-commander/help/sv/Makefile
mini-commander/help/zh_HK/Makefile
mini-commander/help/zh_CN/Makefile
mini-commander/help/zh_TW/Makefile
mixer/Makefile
mixer/docs/Makefile
mixer/docs/C/Makefile
mixer/docs/de/Makefile
mixer/docs/es/Makefile
mixer/docs/eu/Makefile
mixer/docs/fr/Makefile
mixer/docs/it/Makefile
mixer/docs/ja/Makefile
mixer/docs/ko/Makefile
mixer/docs/sv/Makefile
mixer/docs/zh_CN/Makefile
mixer/docs/zh_HK/Makefile
mixer/docs/zh_TW/Makefile
drivemount/Makefile
drivemount/help/Makefile
drivemount/help/C/Makefile
drivemount/help/de/Makefile
drivemount/help/es/Makefile
drivemount/help/eu/Makefile
drivemount/help/fr/Makefile
drivemount/help/it/Makefile
drivemount/help/ja/Makefile
drivemount/help/ko/Makefile
drivemount/help/sv/Makefile
drivemount/help/zh_HK/Makefile
drivemount/help/zh_CN/Makefile
drivemount/help/zh_TW/Makefile
modemlights/Makefile
modemlights/docs/Makefile
modemlights/docs/C/Makefile
modemlights/docs/eu/Makefile
cdplayer/Makefile
cdplayer/docs/Makefile
cdplayer/docs/C/Makefile
cdplayer/docs/de/Makefile
cdplayer/docs/es/Makefile
cdplayer/docs/eu/Makefile
cdplayer/docs/fr/Makefile
cdplayer/docs/it/Makefile
cdplayer/docs/ja/Makefile
cdplayer/docs/ko/Makefile
cdplayer/docs/sv/Makefile
cdplayer/docs/zh_CN/Makefile
cdplayer/docs/zh_HK/Makefile
cdplayer/docs/zh_TW/Makefile
multiload/Makefile
multiload/docs/Makefile
multiload/docs/C/Makefile
multiload/docs/de/Makefile
multiload/docs/es/Makefile
multiload/docs/eu/Makefile
multiload/docs/fr/Makefile
multiload/docs/it/Makefile
multiload/docs/ja/Makefile
multiload/docs/ko/Makefile
multiload/docs/sv/Makefile
multiload/docs/zh_CN/Makefile
multiload/docs/zh_HK/Makefile
multiload/docs/zh_TW/Makefile
gkb-new/Makefile
gkb-new/xmodmap/Makefile
gkb-new/xmodmap.sun/Makefile
charpick/Makefile
charpick/help/Makefile
charpick/help/C/Makefile
charpick/help/de/Makefile
charpick/help/es/Makefile
charpick/help/eu/Makefile
charpick/help/fr/Makefile
charpick/help/it/Makefile
charpick/help/ja/Makefile
charpick/help/ko/Makefile
charpick/help/sv/Makefile
charpick/help/zh_HK/Makefile
charpick/help/zh_CN/Makefile
charpick/help/zh_TW/Makefile
gtik/Makefile
gtik/access/Makefile
gtik/help/Makefile
gtik/help/C/Makefile
gtik/help/de/Makefile
gtik/help/es/Makefile
gtik/help/eu/Makefile
gtik/help/fr/Makefile
gtik/help/it/Makefile
gtik/help/ja/Makefile
gtik/help/ko/Makefile
gtik/help/sv/Makefile
gtik/help/zh_CN/Makefile
gtik/help/zh_HK/Makefile
gtik/help/zh_TW/Makefile
gweather/Makefile
gweather/docs/Makefile
gweather/docs/C/Makefile
gweather/docs/de/Makefile
gweather/docs/es/Makefile
gweather/docs/eu/Makefile
gweather/docs/fr/Makefile
gweather/docs/it/Makefile
gweather/docs/ja/Makefile
gweather/docs/ko/Makefile
gweather/docs/sv/Makefile
gweather/docs/zh_HK/Makefile
gweather/docs/zh_CN/Makefile
gweather/docs/zh_TW/Makefile
geyes/Makefile
geyes/themes/Makefile
geyes/docs/Makefile
geyes/docs/C/Makefile
geyes/docs/de/Makefile
geyes/docs/es/Makefile
geyes/docs/eu/Makefile
geyes/docs/fr/Makefile
geyes/docs/it/Makefile
geyes/docs/ja/Makefile
geyes/docs/ko/Makefile
geyes/docs/sv/Makefile
geyes/docs/zh_CN/Makefile
geyes/docs/zh_HK/Makefile
geyes/docs/zh_TW/Makefile
battstat/Makefile
battstat/apmlib/Makefile
battstat/sounds/Makefile
battstat/docs/Makefile
battstat/docs/C/Makefile
battstat/docs/de/Makefile
battstat/docs/es/Makefile
battstat/docs/eu/Makefile
battstat/docs/fr/Makefile
battstat/docs/it/Makefile
battstat/docs/ja/Makefile
battstat/docs/ko/Makefile
battstat/docs/sv/Makefile
battstat/docs/zh_CN/Makefile
battstat/docs/zh_HK/Makefile
battstat/docs/zh_TW/Makefile
man/Makefile
wireless/Makefile
wireless/docs/Makefile
wireless/docs/C/Makefile
wireless/docs/de/Makefile
wireless/docs/es/Makefile
wireless/docs/eu/Makefile
wireless/docs/fr/Makefile
wireless/docs/it/Makefile
wireless/docs/ja/Makefile
wireless/docs/ko/Makefile
wireless/docs/sv/Makefile
wireless/docs/zh_CN/Makefile
wireless/docs/zh_HK/Makefile
wireless/docs/zh_TW/Makefile
stickynotes/Makefile
stickynotes/pixmaps/Makefile
stickynotes/docs/Makefile
stickynotes/docs/C/Makefile
stickynotes/docs/de/Makefile
stickynotes/docs/es/Makefile
stickynotes/docs/eu/Makefile
stickynotes/docs/fr/Makefile
stickynotes/docs/it/Makefile
stickynotes/docs/ja/Makefile
stickynotes/docs/ko/Makefile
stickynotes/docs/sv/Makefile
stickynotes/docs/zh_CN/Makefile
stickynotes/docs/zh_HK/Makefile
stickynotes/docs/zh_TW/Makefile
libgswitchit/Makefile
libgswitchit/gswitchit.pc
gswitchit/Makefile
gswitchit/help/Makefile
gswitchit/help/C/Makefile
trashapplet/Makefile
trashapplet/src/Makefile
trashapplet/docs/Makefile
trashapplet/docs/C/Makefile
cpufreq/Makefile
cpufreq/src/Makefile
cpufreq/src/cpufreq-selector/Makefile
cpufreq/pixmaps/Makefile
cpufreq/help/Makefile
cpufreq/help/C/Makefile
null_applet/Makefile
])
